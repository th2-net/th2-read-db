plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
}

apply from: '../common.gradle'

sourceCompatibility = 11
targetCompatibility = 11

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

dependencies {
    implementation project(':read-db-core')

    //region Drivers
    runtimeOnly('org.postgresql:postgresql:42.5.3') {
        because('prostresql support')
    }
    runtimeOnly('com.mysql:mysql-connector-j:8.1.0') {
        because('mysql support')
    }
    runtimeOnly('com.oracle.database.jdbc:ojdbc11:21.9.0.0') {
        because('oracle support')
    }
    //endregion
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

application {
    mainClass.set("com.exactpro.th2.read.db.bootstrap.Main")
}

tasks.withType(PublishToMavenRepository).configureEach {it.enabled = false}